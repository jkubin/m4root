__HEADER([Josef Kubin], [2019/12/12], [root_cz])
___DESCR([creates the final file for Content Management System (copy and paste)])
___POINT([template for Content Management System])

# A → β
define([PEREX_IMG], defn([TEST_ATM])[

	# transition to the next node (redefine itself to path)
	define([$0], [$2])
])

# A → β
define([IMG_SRC], [$2])

# relative path to other language during development
# A → β
define([OTHER_LANGUAGE], defn([OTHER_LANG_PATH]))

# path for generated files
define([SRC_FILE_PATH], [https://raw.githubusercontent.com/jkubin/m4root/ARG1(GIT_CSV)/])

# name for repository
define([SRC_REPO_NAME], defn([WORD_SRC])[GitHub
])

m4wrap([

# the resulting html code will be edited by other people (who strongly hate M4 and love drudgery)
# the header helps to maintain consistency: creation_time, source_file.mc, git_rev_of_source_mc_file, git_HEAD_rev
divert(BEGIN_OF_ARTICLE)dnl
<!-- "CAPTION" __SOURCE(LB()ARG1(esyscmd([date '+[%Y%m%d-%R:%S],'])), defn([SOURCE]), ARG1(esyscmd([git log -1 --format='[%h],' ]defn([SOURCE]))), ARG1(esyscmd([git log -1 --format='[%h],']))[]RB()) -->
divert(END_OF_ARTICLE)dnl
<!-- end of html generated by M4 -->
divert(0)dnl
[#] DONTE()

CAPTION

defn(defn([FILE_PREFIX]).anch)

PEREX_IMG

# shortcut for perex: "+yip

PEREX

# shortcut for article: "+yG

])
