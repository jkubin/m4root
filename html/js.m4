__HEADER([Josef Kubin], [2020/03/08], [html])
___DESCR([various JavaScripts for html code])
___POINT([static html generated by M4 and completed on the client side by JavaScript])

# common code for test results
# β
pushdef([TEST_RESULT_OF_AN_EXTERNAL_COMMAND], [

	# stop if an error occurred
	ifelse(sysval, [0], [], [

		ROOT_ERROR([an external command error occured])
	])
])

#      _____________________      __________
# --->/ ADD_JAVASCRIPT_TAGS \--->/ undefine \
#     \_____________________/    \__________/
#
# A → β
define([ADD_JAVASCRIPT_TAGS], [

	undefine([ADD_JAVASCRIPT_TAGS])

	divert(JAVASCRIPT_DISABLED)dnl
<noscript><div title="defn([WORD_INFORMATION])" class="rs-tip-major ADD_CLASS([info])">LANG(dnl
[Srozumitelnost ukázkového zdrojového kódu zvyšuje povolený JavaScript],
[The clarity of the sample source code increases JavaScript enabled]).</div></noscript>
divert(JAVASCRIPT_CODE)dnl
<script>dnl
divert(JAVASCRIPT_CODE_END)dnl
</script>
divert(-1)
])

#      _________________________________      __________
# --->/ ADD_JAVASCRIPT_FOR_LINE_NUMBERS \--->/ undefine \
#     \_________________________________/    \__________/
#
# A → β
define([ADD_JAVASCRIPT_FOR_LINE_NUMBERS], [

	undefine([ADD_JAVASCRIPT_FOR_LINE_NUMBERS])

	ADD_JAVASCRIPT_TAGS()

	divert(JAVASCRIPT_CODE)ARG1(esyscmd([sed -f html/jscompress.sed << EOF

var m4_pre = document.getElementsByTagName("pre");

for (var e of m4_pre) {
	var p = e.parentElement, n = e.innerHTML.split('\n').length, o = document.createElement("ol");

	for (var i = 0; i < n; i++)
		o.appendChild(document.createElement("li"));

	p.insertBefore(o, p.firstChild);
}

EOF]))divert(-1)
]defn([TEST_RESULT_OF_AN_EXTERNAL_COMMAND]))

# forget local β rules (good for frozen files)
popdef(

	[TEST_RESULT_OF_AN_EXTERNAL_COMMAND],

)
